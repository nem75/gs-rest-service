buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.5.RELEASE")
        classpath('gradle.plugin.com.palantir.gradle.docker:gradle-docker:0.+')
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'com.palantir.docker'
apply plugin: 'com.palantir.docker-compose'

group = 'nem75'
version = '0.1.0'

docker {
    name "${project.group}/${bootJar.baseName}"
    tag "asLatest", "${project.group}/${bootJar.baseName}:latest"
    tag "withVersion", "${project.group}/${bootJar.baseName}:${version}"
    pull true
    files tasks.bootJar.outputs
}

dockerCompose {
    template 'docker-compose.template.yaml'
    dockerComposeFile 'docker-compose.yaml'
}

task up {
    dependsOn build, dockerTag, generateDockerCompose, dockerComposeUp
}

task down(type:Exec) {
    group = 'docker'
    commandLine 'docker-compose', 'down'
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.postgresql:postgresql')
    testCompile('org.springframework.boot:spring-boot-starter-test')
    docker("${group}:${bootJar.baseName}:${version}")
}
